plugins {
    id "com.moowork.node" version "0.9"
    id "com.moowork.grunt" version "0.9"
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.moowork.node'
apply plugin: 'com.moowork.grunt'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// set default encoding
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

mainClassName = 'org.bitionaire.elbombillo.ui.WebServer'

dependencies {
    compile group: 'org.eclipse.jetty', name: 'jetty-webapp', version: '9.2.6.v20141205'
    compile group: 'org.eclipse.jetty', name: 'jetty-proxy', version: '9.2.6.v20141205'

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.7'
    compile group: 'args4j', name: 'args4j', version: '2.0.29'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.14.8'
    compile group: 'com.google.guava', name: 'guava', version: '18.0'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

sourceSets {
    main {
        resources.exclude '**/app/', '**/sass/'
    }
}

// --------------------------------------------------------------------------------------------------------------------
// Development tasks
// --------------------------------------------------------------------------------------------------------------------
node {
    version = '0.12.4'
    npmVersion = '2.11.0'
    download = true
}

// bower and cleanup
task bowerInstall(type: NodeTask) {
    script = project.file('node_modules/bower/bin/bower')
    args = ['install']
}
bowerInstall.dependsOn npmInstall
clean { delete 'bower_components' }

// grunt configuration
task gruntBuild(type: GruntTask) {
    dependsOn 'installGrunt', 'npmInstall', 'bowerInstall'
    args = [ "default" ]
}
processResources.dependsOn gruntBuild

task gruntWatch(type: GruntBackgroundTask) {
    dependsOn 'gruntBuild'
    args = [ "watch" ]
}

// execution of webserver
run {
    dependsOn gruntWatch
    args '--port', '8080'
}

class GruntBackgroundTask extends com.moowork.gradle.grunt.GruntTask {
    @Override
    void exec() {
        Thread.start {
            super.exec()
        }
    }
}